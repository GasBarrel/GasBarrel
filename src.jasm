public class JDABot version 52:0 {
	public static Method main:"([Ljava/lang/String;)V" stack 6 locals 1 {
		stack_frame_type full;
		locals_map class "[Ljava/lang/String;";
		ldc "<token>";
		invokestatic net/dv8tion/jda/api/JDABuilder.createLight:"(Ljava/lang/String;)Lnet/dv8tion/jda/api/JDABuilder;";
		iconst_1;
		anewarray java/lang/Object;
		dup;
		iconst_0;
		new Ping;
		dup;
		invokespecial Ping."<init>":"()V";
		aastore;
		invokevirtual net/dv8tion/jda/api/JDABuilder.addEventListeners:"([Ljava/lang/Object;)Lnet/dv8tion/jda/api/JDABuilder;";
		invokevirtual net/dv8tion/jda/api/JDABuilder.build:"()Lnet/dv8tion/jda/api/JDA;";
		return;
	}
}

public class Ping extends net/dv8tion/jda/api/hooks/ListenerAdapter version 52:0 {
	public Method "<init>":"()V" stack 2 locals 1 {
		stack_frame_type full;
		locals_map this;
		aload 0;
		invokespecial net/dv8tion/jda/api/hooks/ListenerAdapter."<init>":()V;
		return;
	}
	
	public Method onMessageReceived:"(Lnet/dv8tion/jda/api/events/message/MessageReceivedEvent;)V" stack 4 locals 2 {
		stack_frame_type full;
		locals_map class "Ping", class "net/dv8tion/jda/api/events/message/MessageReceivedEvent";
		aload 1;
		invokevirtual net/dv8tion/jda/api/events/message/MessageReceivedEvent.getMessage:"()Lnet/dv8tion/jda/api/entities/Message;";
		invokeinterface net/dv8tion/jda/api/entities/Message.getContentRaw:"()Ljava/lang/String;",1;
		ldc "!ping";
		invokevirtual java/lang/String.contains:"(Ljava/lang/CharSequence;)Z";
		aload 1;
		invokevirtual net/dv8tion/jda/api/events/message/MessageReceivedEvent.getAuthor:"()Lnet/dv8tion/jda/api/entities/User;";
		invokeinterface net/dv8tion/jda/api/entities/User.isBot:"()Z",1;
		iconst_1;
		ixor;
		iand;
		iconst_1;
		if_icmpeq execPong;
		return;
	execPong:
		stack_frame_type full;
		//stack_map bogus;
		locals_map class "Ping", class "net/dv8tion/jda/api/events/message/MessageReceivedEvent";
		aload 1;
		invokevirtual net/dv8tion/jda/api/events/message/GenericMessageEvent.getChannel:"()Lnet/dv8tion/jda/api/entities/MessageChannel;";
		ldc "Pong!";
		invokeinterface net/dv8tion/jda/api/entities/MessageChannel.sendMessage:"(Ljava/lang/CharSequence;)Lnet/dv8tion/jda/api/requests/restaction/MessageAction;",2;
		invokeinterface net/dv8tion/jda/api/requests/RestAction.queue:"()V",1;
		return;
	}
}